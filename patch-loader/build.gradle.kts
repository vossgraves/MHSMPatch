import java.util.Locale

plugins {
    alias(libs.plugins.agp.app)
}

android {
    defaultConfig {
        multiDexEnabled = false

        externalNativeBuild {
            cmake {
                arguments += "-DCORE_ROOT=${File(rootDir.absolutePath, "core/native") }"
                arguments += "-DEXTERNAL_ROOT=${File(rootDir.absolutePath, "core/external") }"
                arguments += "-DVERSION_CODE=${rootProject.extra["verCode"]}"
                arguments += "-DVERSION_NAME=${rootProject.extra["verName"]}"
            }
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }

    externalNativeBuild {
        cmake {
            path("src/main/jni/CMakeLists.txt")
            version = "3.31.6"
        }
    }

    packaging {
        dex {
            useLegacyPackaging = true
        }
    }
    namespace = "top.winner02.spotmanager.loader"
}

androidComponents.onVariants { variant ->
    val variantCapped = variant.name.replaceFirstChar { it.uppercase() }

    val copyDexTask = tasks.register<Copy>("copyDex$variantCapped") {
        dependsOn("assemble$variantCapped")
        doFirst {
            delete("${rootProject.projectDir}/out/assets/${variant.name}/spotmanager/loader.dex")
        }
        from(layout.buildDirectory.file("intermediates/dex/${variant.name}/mergeDex$variantCapped/classes.dex"))
        rename("classes.dex", "loader.bin")
        into("${rootProject.projectDir}/out/assets/${variant.name}/spotmanager")
    }

    val copySoTask = tasks.register<Copy>("copySo$variantCapped") {
        dependsOn("assemble$variantCapped")
        dependsOn("strip${variantCapped}DebugSymbols")
        from(
            fileTree(
                "dir" to layout.buildDirectory.dir("intermediates/stripped_native_libs/${variant.name}/strip${variantCapped}DebugSymbols/out/lib"),
                "include" to listOf("**/libspotmanager.so")
            )
        )
        into("${rootProject.projectDir}/out/assets/${variant.name}/spotmanager/so")
    }

    tasks.register("copy$variantCapped") {
        dependsOn(copySoTask)
        dependsOn(copyDexTask)

        doLast {
            println("Dex and so files has been copied to ${rootProject.projectDir}${File.separator}out")
        }
    }
}

dependencies {
    compileOnly("vector:stubs")
    implementation("vector:core")
    implementation("vector:bridge")
    implementation("vector:daemon-service")
    implementation("vector:legacy")
    implementation(projects.share.android)
    implementation(projects.share.java)

    implementation(libs.gson)
}
